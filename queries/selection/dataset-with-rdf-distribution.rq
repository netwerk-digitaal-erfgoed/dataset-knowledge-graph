PREFIX dcat: <http://www.w3.org/ns/dcat#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX schema: <http://schema.org/>

CONSTRUCT {
    ?dataset a dcat:Dataset ;
        dcat:distribution ?distribution .

    ?distribution a dcat:Distribution ;
        dcat:accessURL ?distribution_url ;
        dcat:mediaType ?distribution_mediaType ;
        dcat:byteSize ?distribution_size ;
        dct:modified ?distribution_modified .
} WHERE {
    ?dataset a dcat:Dataset ;
        dcat:distribution ?distribution .

    ?distribution dcat:accessURL ?distribution_url ;
        dcat:mediaType ?distribution_mediaType .

    OPTIONAL { ?distribution dcat:byteSize ?distribution_size . }
    OPTIONAL { ?distribution dct:modified ?distribution_modified . }
    FILTER(
        ?distribution_mediaType = "application/sparql-query"
        || ?distribution_mediaType = "application/sparql-results+json"
        || ?distribution_mediaType = "application/ld+json"
        || ?distribution_mediaType = "application/ld+json+gzip"
        || ?distribution_mediaType = "application/n-quads"
        || ?distribution_mediaType = "application/n-quads+gzip"
        || ?distribution_mediaType = "application/n-triples"
        || ?distribution_mediaType = "application/n-triples+gzip"
        || ?distribution_mediaType = "text/turtle"
        || ?distribution_mediaType = "text/turtle+gzip"
    )

    # Exclude dataset descriptions that are no longer valid.
    FILTER(NOT EXISTS {
        ?dataset schema:subjectOf ?registrationUrl .
        ?registrationUrl schema:validUntil [] .
    })

    # In case of a catalog, exclude datasets that are no longer part of the catalog.
    OPTIONAL {
        SELECT ?dataset (MAX(?datasetRead) AS ?datasetLastRead) (MAX(?registrationUrlRead) AS ?registrationUrlLastRead) {
            ?dataset schema:dateRead ?datasetRead ;
                schema:subjectOf ?registrationUrl .
            ?registrationUrl schema:dateRead ?registrationUrlRead .
        }
        GROUP BY ?dataset
    }
    FILTER(
        (!BOUND(?datasetLastRead) && !BOUND(?registrationUrlLastRead)) # Without Dataset Register metadata, for local tests.
        || ?datasetLastRead >= ?registrationUrlLastRead
    )

    # Skip unreliable endpoints.
    FILTER(!STRSTARTS(STR(?distribution_url), "https://cat.apis.beeldengeluid.nl/sparql"))
} ORDER BY ?dataset ?distribution
