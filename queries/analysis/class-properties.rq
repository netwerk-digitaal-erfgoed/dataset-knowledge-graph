PREFIX void: <http://rdfs.org/ns/void#>

CONSTRUCT {
    ?dataset a void:Dataset ;
        void:classPartition ?classPartition .
    ?classPartition void:class ?type ;
        void:propertyPartition [
            void:property ?p ;
            void:entities ?subjects ;
            void:distinctObjects ?objects ;
        ] .
}
#namedGraph#
WHERE {
    # Split subject and object counts into separate subqueries to avoid memory issues
    # from the Cartesian product of types × properties × objects.
    {
        SELECT ?type ?p (COUNT(DISTINCT ?s) AS ?subjects) {
            #subjectFilter#
            ?s a ?type ; ?p [] .
        }
        GROUP BY ?type ?p
    }
    {
        SELECT ?type ?p (COUNT(DISTINCT ?o) AS ?objects) {
            #subjectFilter#
            ?s a ?type ; ?p ?o .
        }
        GROUP BY ?type ?p
    }
    BIND(URI(CONCAT("#class-", MD5(STR(?type)))) as ?classPartition)
}
LIMIT 100000
