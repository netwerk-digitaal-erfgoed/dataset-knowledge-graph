PREFIX void: <http://rdfs.org/ns/void#>

CONSTRUCT {
    ?dataset a void:Dataset ;
        void:classPartition ?classPartition .
    ?classPartition void:class ?type ;
        void:propertyPartition [
            void:property ?p ;
            void:entities ?subjects ;
            void:distinctObjects ?objects ;
        ] .
}
#namedGraph#
WHERE {
    {
        SELECT ?type ?p (COUNT(DISTINCT ?s) AS ?subjects) (COUNT(DISTINCT ?o) AS ?objects) {
            #subjectFilter#
            ?s a ?type ;
                ?p ?o .
        }
        GROUP BY ?type ?p
    }
    BIND(URI(CONCAT("#class-", MD5(STR(?type)))) as ?classPartition)
}
LIMIT 100000
