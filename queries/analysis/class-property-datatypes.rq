PREFIX void: <http://rdfs.org/ns/void#>
PREFIX void-ext: <http://ldf.fi/void-ext#>

CONSTRUCT {
  ?propertyPartition void-ext:datatypePartition ?datatypePartition .
  ?datatypePartition
      void-ext:datatype ?dt ;
      void:triples ?count .
}
#namedGraph#
WHERE {
  {
    SELECT ?p ?dt (COUNT(*) AS ?count) {
      {
        SELECT ?p ?o {
          #subjectFilter#
          ?s a <#class#> ;
            ?p ?o .
          FILTER (ISLITERAL(?o))
        }
      }
      BIND(DATATYPE(?o) AS ?dt)
    }
    GROUP BY ?p ?dt
  }
  BIND(URI(CONCAT(STR(?dataset), "/.well-known/void#class-property-", MD5(CONCAT(STR(<#class#>), STR(?p))))) AS ?propertyPartition)
  BIND(URI(CONCAT(STR(?dataset), "/.well-known/void#datatype-", MD5(CONCAT(STR(<#class#>), STR(?p), STR(?dt))))) AS ?datatypePartition)
}
LIMIT 100000
