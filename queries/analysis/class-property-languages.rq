PREFIX void: <http://rdfs.org/ns/void#>
PREFIX void-ext: <http://ldf.fi/void-ext#>

CONSTRUCT {
  ?propertyPartition void-ext:languagePartition ?languagePartition .
  ?languagePartition
    void-ext:language ?lang ;
    void:triples ?count .
}
#namedGraph#
WHERE {
  {
    SELECT ?p ?lang (COUNT(*) AS ?count) {
      {
        # Pre-filter to distinct literals to minimize LANG calls
        SELECT DISTINCT ?p ?o {
          #subjectFilter#
          ?s a <#class#> ;
            ?p ?o .
          FILTER(ISLITERAL(?o))
        }
      }
      BIND(LANG(?o) AS ?lang)
      FILTER(?lang != "")
    }
    GROUP BY ?p ?lang
  }
  BIND(URI(CONCAT(STR(?dataset), "/.well-known/void#class-property-", MD5(CONCAT(STR(<#class#>), STR(?p))))) AS ?propertyPartition)
  BIND(URI(CONCAT(STR(?dataset), "/.well-known/void#language-", MD5(CONCAT(STR(<#class#>), STR(?p), ?lang)))) AS ?languagePartition)
}
LIMIT 100000
