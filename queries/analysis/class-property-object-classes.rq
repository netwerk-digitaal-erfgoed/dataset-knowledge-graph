PREFIX void: <http://rdfs.org/ns/void#>
PREFIX void-ext: <http://ldf.fi/void-ext#>

CONSTRUCT {
  ?propertyPartition void-ext:objectClassPartition ?objectClassPartition .
  ?objectClassPartition
      void:class ?objectClass ;
      void:triples ?count .
}
#namedGraph#
WHERE {
  {
    SELECT ?p ?objectClass (COUNT(*) AS ?count) {
      {
        SELECT ?p ?o {
          #subjectFilter#
          ?s a <#class#> ;
            ?p ?o .
          FILTER (ISIRI(?o))
        }
      }
      ?o a ?objectClass .
    }
    GROUP BY ?p ?objectClass
  }
  BIND(URI(CONCAT(STR(?dataset), "/.well-known/void#class-property-", MD5(CONCAT(STR(<#class#>), STR(?p))))) AS ?propertyPartition)
  BIND(URI(CONCAT(STR(?dataset), "/.well-known/void#object-class-", MD5(CONCAT(STR(<#class#>), STR(?p), STR(?objectClass))))) AS ?objectClassPartition)
}
LIMIT 100000
